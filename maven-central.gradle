// gradle clean install -Dorg.gradle.project.version=x.x.x

apply plugin: 'signing'
apply plugin: 'maven'

if (!project.hasProperty('ossrhUsername')) {
    println 'Signing configuration is missing, skip signing'
    return
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment {
            MavenDeployment deployment -> signing.signPom(deployment)
        }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
            name 'RxJersey'
            description 'RxJava extension for Jersey and Dropwizard'
            url 'https://github.com/alex-shpak/rx-jersey'
            packaging 'jar'

            scm {
                connection 'scm:git:git://github.com/alex-shpak/rx-jersey.git'
                developerConnection 'scm:git:ssh://github.com:alex-shpak/rx-jersey.git'
                url 'https://github.com/alex-shpak/rx-jersey'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'http://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id = '0x719d67fc'
                    name = 'Alex Shpak'
                }
            }
        }
    }
}
